library Roll

class PrintResults : Visitor
  PROPERTIES
    line = String()

  METHODS
    method on_visit_node( cmd:Add )
      visit( cmd.left )
      line.print " + "
      visit( cmd.right )

    method on_visit_node( cmd:Dice )
      if (line.count) line.print ' '
      line.print "$ $"(cmd.name,cmd.values)

    method on_visit_node( cmd:Divide )
      visit( cmd.left )
      line.print " / "
      visit( cmd.right )

    method on_visit_node( cmd:Multiply )
      visit( cmd.left )
      line.print " * "
      visit( cmd.right )

    method on_visit_node( cmd:Number )
      line.print cmd.value

    method on_visit_node( cmd:Subtract )
      visit( cmd.left )
      line.print " - "
      visit( cmd.right )

    method on_visit( cmd:Statements )->Cmd
      forEach (statement in cmd)
        line.clear
        visit( statement )
        print line
        println ": $"(statement->Int)
      endForEach
      return cmd

endClass
